DIGITS = range(61, 81)
SEEDS = ('042', '069', '151', '401', '404')
NOISE = ("0.010", "0.020", "0.030", "0.040")
from glob import glob
import os
from pathlib import Path
import re

include: "rules/ACDC.smk"
include: "rules/simulation.smk"
include: "rules/subject.smk"
include: "rules/figures.smk"

subject_data = glob("data/subject_data/*/*[!mask].nii.gz")

FOLDER = [os.path.basename(Path(x).parent) for x in subject_data]

wildcard_constraints:
    seed= '|'.join([re.escape(x) for x in SEEDS]),
    folder= '|'.join([re.escape(x) for x in FOLDER]),
    noise= '|'.join([re.escape(x) for x in NOISE])

rule all:
    input:
        "analysis/plots/mrxcat_simulation/features_curves/top12_features.png",
        "analysis/plots/mrxcat_simulation/total_mae_vs_snr_mae.png",
        "analysis/plots/mrxcat_simulation/total_mae_vs_snr_mae_1.png",
        "analysis/plots/mrxcat_simulation/rank_barcode.png",
        expand("analysis/plots/subject/features_curves/{folder}/top12_features.png", folder = FOLDER),
        "analysis/plots/subject/total_mae_vs_snr_mae.png",
        "analysis/plots/subject/total_mae_vs_snr_mae_1.png",
        "analysis/plots/subject/rank_barcode.png",
        expand("analysis/plots/ACDC/features_curves/patient0{digits}/top12_features.png", digits = DIGITS),
        "analysis/plots/ACDC/total_mae_vs_snr_mae.png",
        "analysis/plots/ACDC/total_mae_vs_snr_mae_1.png",
        "analysis/plots/ACDC/rank_barcode.png",
        expand("figures/fig{num}.svg", num=[2, 3, 4, 5]),
        expand("figures/supp_fig{num}.svg", num=[1, 2]),
        "figures/fig3_supplement.pdf"

rule combine_tables:
    input:
        "analysis/features_normalized/mrxcat_simulation/features.csv",
        "analysis/tables/mrxcat_simulation/ranks.csv",
        expand("analysis/features_normalized/ACDC/patient0{digits}.csv", digits = DIGITS),
        expand("analysis/tables/ACDC/ranks_patient0{digits}.csv", digits = DIGITS),
        expand("analysis/features_normalized/subject/{folder}.csv", folder = FOLDER),
        expand("analysis/tables/subject/ranks_{folder}.csv", folder = FOLDER)
    output:
        "analysis/combined/features.tsv",
        "analysis/combined/ranks.tsv"
    conda:
        "tidyverse.yaml"
    script:
        "../code/combine_features.R"

rule GenerateSuppTable1:
    input:
        "analysis/calculated_mae/mrxcat_simulation/mae.csv",
        expand("analysis/calculated_mae/ACDC/mae_patient0{digits}.csv", digits = DIGITS),
        expand("analysis/calculated_mae/subject/mae_{folder}.csv", folder = FOLDER)
    output:
        "figures/supp_tab1.tsv"
    conda:
        "tidyverse.yaml"
    script:
        "../code/supp_tab1.R"